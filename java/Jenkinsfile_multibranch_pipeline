#!groovy
pipeline {
    agent none
    environment {
        // Need to replace the '%2F' used by Jenkins to deal with / in the path (e.g. story/...)
        theBranch = "${env.BRANCH_NAME}".replace("%2F", "-").replace("/", "-")
        theVersion = "0-${theBranch}-SNAPSHOT"
        dittoVersion = "${theVersion}"
        theMvnRepo = "$WORKSPACE/../feature-repository-${theBranch}";
        JAVA_TOOL_OPTIONS = '-Duser.home=/home/jenkins-slave'
    }
    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
        disableConcurrentBuilds()
    }
    stages {
        stage('Choose Ditto Version to build against') {
            steps {
                script {
                    dittoVersion = askForDittoVersionToUse(dittoVersion)
                    echo "Selected Ditto version: ${dittoVersion}"
                }
            }
        }
        stage('Checkout scm') {
            agent {
                label "$DITTO_BUILD_NODE"
            }
            steps {
                echo 'Checkout scm'
                checkout scm
            }
        }
        stage('Build') {
            agent {
                docker {
                    args "$DITTO_DOCKER_ARGS"
                    image "$DITTO_DOCKER_IMAGE_MAVEN_JDK_8"
                }
            }
            steps {
                dir("$WORKSPACE/java") {
                    configFileProvider([configFile(fileId: 'mvn-bdc-settings', variable: 'MVN_SETTINGS')]) {
                        sh "mvn -s $MVN_SETTINGS clean deploy javadoc:jar source:jar" +
                                " --batch-mode --errors" +
                                " -DcreateJavadoc=true" +
                                " -Drevision=${theVersion}" +
                                " -Dditto.version=${dittoVersion}" +
                                " -Pditto-java-client"
                    }
                }
            }
        }
        stage ('SonarQube') {
            agent {
                docker {
                    args "$DITTO_DOCKER_ARGS"
                    image "$DITTO_DOCKER_IMAGE_MAVEN_JDK_17"
                    reuseNode true
                }
            }
            when {
                expression {
                    return !params.SKIP_SONAR_SCAN
                }
            }
            steps {
                dir("$WORKSPACE/java") {
                    configFileProvider([configFile(fileId: 'mvn-bdc-settings', variable: 'MVN_SETTINGS')]) {
                        echo 'Sonar Scan'
                        withSonarQubeEnv(installationName: "${env.SONAR_QUBE_ENV}", credentialsId: 'sonarqube') {
                            sh "mvn -s $MVN_SETTINGS --batch-mode --errors $SONAR_MAVEN_GOAL " +
                                    "-Dsonar.host.url=$SONAR_HOST_URL " +
                                    "-Dsonar.projectKey=org.eclipse.ditto:${theBranch} " +
                                    "-Drevision=${theVersion} " +
                                    "-Dditto.version=${dittoVersion} "
                        }
                    }
                }
            }
        }
    }
}

def askForDittoVersionToUse(defaultVersion) {
    try {
        timeout(time: 1, unit: 'MINUTES') {
            def branch = input message: 'Which Ditto version to use?',
                    parameters: [string(defaultValue: defaultVersion, description: 'Branch', name: 'DITTO-BRANCH')]
            return branch
        }
    } catch (e) {
        return defaultVersion
    }
}